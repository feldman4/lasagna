from glob import glob
import json

import snakemake
# from lasagna.snakes.firesnake3 import call_firesnake
from firesnake3 import call_firesnake


# if true, will run when the data is complete, doing what it can
# snakemake.ignore_missing = True

# UPDATE: set to number of cycles used
CYCLES = ['c{cycle}-SBS-{cycle}'.format(cycle=c) for c in range(1, 13)]
CYCLES = CYCLES[:7]

THRESHOLD_DO = 2000
INDEX_DO = None


# UPDATE: more sites will take longer for the DAG to build (especially if ignore_missing is true)
WELLS = ['A1', 'A2', 'A3', 'A4']


RANGE = lambda x: list(map(str, range(x[0] * x[1])))
TILES_SBS = RANGE((7, 7))
TILES = RANGE((6, 6))

missing = list(map(str, [0, 6, 12, 18, 24, 30]))
TILES = [t for t in TILES if t not in missing]

DISPLAY_RANGES=((500, 10000), 
                (500, 5000), 
                (500, 5000), 
                (500, 5000), 
                (500, 5000))


CHANNEL_OFFSETS = [(0, 0), (0, 0), (0, 0), (0, 0), (0, 0)]
RING_WIDTH = 3
MEDIAN_WIDTH = 7
THRESHOLD_DAPI = 1000
THRESHOLD_DAPI_LIVE = 2600 

PHENOTYPE_TIF = 'process/10X_{well}_Tile-{tile}.live_final.tif'
PHENOTYPE_LIVE_TIF = 'process/10X_{well}_Tile-{tile}.live_aligned.tif'
CYCLE_LIVE = 'c0-Hoechst-mNeon'


rule all:
    input:
    # can request individual files here
        expand('process/10X_{well}_Tile-{tile}.aligned.tif', well=WELLS, tile=TILES),
        expand('process/10X_{well}_Tile-{tile}.live_aligned.tif', well=WELLS, tile=TILES),
        # expand('process/10X_{well}_Tile-{tile}.phenotype_live.pkl', well=WELLS, tile=TILES)
        # expand('process/10X_{well}_Tile-{tile}.SBS_table.pkl', well=WELLS, tile=TILES)

rule segment_nuclei:
    input:
        'process/10X_{well}_Tile-{tile}.c1_SBS.tif',
        # discarded input, to change run order
        'process/10X_{well}_Tile-{tile}.aligned_SBS.tif'
    output:
        'process/10X_{well}_Tile-{tile}.nuclei.tif'
    run:
        call_firesnake('segment_nuclei', output, data=input[0], 
            threshold=THRESHOLD_DAPI, area_min=0.25*150, area_max=0.25*800)

rule segment_cells:
    """segment_cells works best when fed data with the most cytoplasmic autofluorescence. SBS data is OK but
    not the best if something else is available.
    """
    input:
        'process/10X_{well}_Tile-{tile}.c1_SBS.tif',
        'process/10X_{well}_Tile-{tile}.nuclei.tif'
    output:
        'process/10X_{well}_Tile-{tile}.cells.tif'
    run:
        call_firesnake('segment_cells', output, 
            data=input[0], nuclei=input[1], threshold=4*200)


rule segment_nuclei_phenotype:
    priority: -1
    input:
        PHENOTYPE_TIF
    output:
        'process/10X_{well}_Tile-{tile}.nuclei_final.tif'
    run:
        call_firesnake('segment_nuclei', output, data=input[0], 
            threshold=THRESHOLD_DAPI_LIVE, area_min=0.25*150, area_max=0.25*800)

rule segment_cells_phenotype:
    """segment_cells works best when fed data with the most cytoplasmic autofluorescence. SBS data is OK but
    not the best if something else is available.
    """
    input:
        PHENOTYPE_TIF,
        'process/10X_{well}_Tile-{tile}.nuclei_final.tif'
    output:
        'process/10X_{well}_Tile-{tile}.cells_final.tif'
    run:
        call_firesnake('segment_cells_bsub', output, 
            data=input[0], nuclei=input[1], threshold=4*200)

rule align:
    priority: -1
    input:
        expand('process/10X_{cycle}/10X_{cycle}_{{well}}_Tile-{{tile}}.max.tif', cycle=CYCLES)
    output:
        'process/10X_{well}_Tile-{tile}.aligned.tif'
    run:
        call_firesnake('align_DAPI_first', output, data=input,
                display_ranges=DISPLAY_RANGES)

rule align_live:
    priority: -1
    input:
        'process/10X_{cycle}/10X_{cycle}_{{well}}_Tile-{{tile}}.live.tif'.format(cycle=CYCLE_LIVE)
    output:
        'process/10X_{well}_Tile-{tile}.live_aligned.tif'
    run:
        call_firesnake('align_one_stack', output, data=input)

rule transform_LoG:
    input:
        'process/10X_{well}_Tile-{tile}.aligned_SBS.tif'
    output:
        'process/10X_{well}_Tile-{tile}.log.tif'
    run:
        call_firesnake('transform_LoG', output, data=input[0], 
            display_ranges=DISPLAY_RANGES)

rule consensus_DO:
    input:
        'process/10X_{well}_Tile-{tile}.log.tif'
    output:
        temp('process/10X_{well}_Tile-{tile}.consensus_DO.tif')
    run:
        call_firesnake('consensus_DO', output, data=input[0])

rule find_peaks:
    input:
        'process/10X_{well}_Tile-{tile}.consensus_DO.tif'
    output:
        'process/10X_{well}_Tile-{tile}.peaks.tif'
    run:
        call_firesnake('find_peaks', output, data=input[0], display_ranges=None) 

rule max_filter:
    input:
        'process/10X_{well}_Tile-{tile}.log.tif'
    output:
        'process/10X_{well}_Tile-{tile}.maxed.tif'
    run:
        call_firesnake('max_filter', output, 
            data=input[0], width=3, display_ranges=DISPLAY_RANGES) 

rule extract_barcodes:
    input:
        'process/10X_{well}_Tile-{tile}.peaks.tif',
        'process/10X_{well}_Tile-{tile}.maxed.tif',
        'process/10X_{well}_Tile-{tile}.cells.tif'
    output:
        'process/10X_{well}_Tile-{tile}.barcodes.csv'
    run:
        call_firesnake('extract_barcodes', output, 
            peaks=input[0], data_max=input[1], cells=input[2], 
            cycles=CYCLES, threshold_DO=THRESHOLD_DO, index_DO=INDEX_DO,
            wildcards=dict(wildcards)) 

rule extract_phenotype:
    input:
        PHENOTYPE_TIF,
        'process/10X_{well}_Tile-{tile}.nuclei_final.tif',
        'process/10X_{well}_Tile-{tile}.cells_final.tif'
    output:
        'process/10X_{well}_Tile-{tile}.phenotype_final.pkl'
    run:
        call_firesnake('extract_phenotype_translocation', output, 
            data_phenotype=input[0], nuclei=input[1], cells=input[2], wildcards=dict(wildcards))


rule extract_phenotype_live_translocation:
    input:
        PHENOTYPE_LIVE_TIF,
        'process/10X_{well}_Tile-{tile}.nuclei_final.tif',
        'process/10X_{well}_Tile-{tile}.cells_final.tif'
    output:
        'process/10X_{well}_Tile-{tile}.phenotype_live.pkl'
    run:
        call_firesnake('extract_phenotype_live_translocation', output, 
            data_phenotype=input[0], nuclei=input[1], cells=input[2], wildcards=dict(wildcards))

rule extract_SBS_table:
    input:
        PHENOTYPE_TIF,
        'process/10X_{well}_Tile-{tile}.nuclei.tif'
    output:
        'process/10X_{well}_Tile-{tile}.SBS_table.pkl'
    run:
        call_firesnake('extract_minimal_phenotype', output, 
            data_phenotype=input[0], nuclei=input[1], wildcards=dict(wildcards))
