from glob import glob
import json

import snakemake
# from lasagna.snakes.firesnake3 import call_firesnake
from firesnake3 import call_firesnake, load_well_tile_list


# if true, will run when the data is complete, doing what it can
# snakemake.ignore_missing = True

# UPDATE: set to number of cycles used
CYCLES = ['c{cycle}-SBS-{cycle}'.format(cycle=c) for c in range(1, 13)]
CYCLES = CYCLES[:4]

THRESHOLD_DO = 1000
INDEX_DO = None


# UPDATE: more Tiles will take longer for the DAG to build (especially if ignore_missing is true)
WELLS_, TILES_ = load_well_tile_list('well_tile_list_process.pkl')
WELLS_ = WELLS_
TILES_ = TILES_

DISPLAY_RANGES=((500, 20000), 
                (100, 20000), 
                (100, 20000), 
                (100, 20000), 
                (100, 20000))


CYCLE_PHENOTYPE = 'c0-DAPI-HA'

rule all:
    input:
    # can request individual files here
        expand('process/10X_{well}_Tile-{tile}.barcodes.csv', zip, well=WELLS_, tile=TILES_),
	    expand('process/10X_{well}_Tile-{tile}.phenotype.csv', zip, well=WELLS_, tile=TILES_),
	    expand('process/10X_{well}_Tile-{tile}.cells.csv', zip, well=WELLS_, tile=TILES_),
        # expand('process/10X_{well}_Tile-{tile}.SBS_cell_info.csv', zip, well=WELLS_, tile=TILES_)

rule segment_nuclei:
    input:
        'process/10X_{cycle}/10X_{cycle}_{{well}}_Tile-{{tile}}.max.tif'.format(cycle=CYCLES[0]),
        # discarded input, to change run order
        'process/10X_{well}_Tile-{tile}.log.tif'
    output:
        'process/10X_{well}_Tile-{tile}.nuclei.tif'
    run:
        call_firesnake('segment_nuclei', output, data=input[0], 
            threshold=1200, area_min=0.25*150, area_max=0.25*800)

rule segment_cells:
    """segment_cells works best when fed data with the most cytoplasmic autofluorescence. SBS data is OK but
    not the best if something else is available.
    """
    input:
        expand('process/10X_{cycle}/10X_{cycle}_{{well}}_Tile-{{tile}}.max.tif', cycle=CYCLES[0]),
        'process/10X_{well}_Tile-{tile}.nuclei.tif'
    output:
        'process/10X_{well}_Tile-{tile}.cells.tif'
    run:
        call_firesnake('segment_cells', output, 
            data=input[0], nuclei=input[1], threshold=4*200)

rule align:
    priority: -1
    input:
        expand('process/10X_{cycle}/10X_{cycle}_{{well}}_Tile-{{tile}}.max.tif', cycle=CYCLES)
    output:
        'process/10X_{well}_Tile-{tile}.log.tif'
    run:
        call_firesnake('align_log_SBS_stack', output, data=input,
            display_ranges=DISPLAY_RANGES)

rule check_cy3_quality:
    input:
        'process/10X_{cycle}/10X_{cycle}_{{well}}_Tile-{{tile}}.max.tif'.format(cycle=CYCLES[0])
    output:
        'process/10X_{well}_Tile-{tile}.cy3_quality.csv'
    run:
        call_firesnake('check_cy3_quality', output, data=input[0], wildcards=dict(wildcards))

rule consensus_DO:
    input:
        'process/10X_{well}_Tile-{tile}.log.tif'
    output:
        'process/10X_{well}_Tile-{tile}.consensus_DO.tif'
    run:
        call_firesnake('consensus_DO', output, data=input[0])

rule find_peaks:
    input:
        'process/10X_{well}_Tile-{tile}.consensus_DO.tif'
    output:
        'process/10X_{well}_Tile-{tile}.peaks.tif'
    run:
        call_firesnake('find_peaks', output, data=input[0], display_ranges=None) 

rule max_filter:
    input:
        'process/10X_{well}_Tile-{tile}.log.tif'
    output:
        temp('process/10X_{well}_Tile-{tile}.maxed.tif')
    run:
        call_firesnake('max_filter', output, 
            data=input[0], width=3, display_ranges=DISPLAY_RANGES) 

rule extract_barcodes:
    input:
        'process/10X_{well}_Tile-{tile}.peaks.tif',
        'process/10X_{well}_Tile-{tile}.maxed.tif',
        'process/10X_{well}_Tile-{tile}.cells.tif'
    output:
        'process/10X_{well}_Tile-{tile}.barcodes.csv'
    run:
        call_firesnake('extract_barcodes', output, 
            peaks=input[0], data_max=input[1], cells=input[2], 
            cycles=CYCLES, threshold_DO=THRESHOLD_DO, index_DO=INDEX_DO,
            wildcards=dict(wildcards)) 

rule call_cells:
	input:
		'process/10X_{well}_Tile-{tile}.barcodes.csv'
	output:
		'process/10X_{well}_Tile-{tile}.cells.csv'
	run:
		call_firesnake('call_cells', output, cycles=len(CYCLES),
			df_barcodes=input[0])

rule extract_SBS_locations:
    input:
        'process/10X_{well}_Tile-{tile}.nuclei.tif'
    output:
        'process/10X_{well}_Tile-{tile}.SBS_cell_info.csv'
    run:
        call_firesnake('extract_minimal_phenotype', output, 
            data_phenotype=input[0], nuclei=input[0], wildcards=dict(wildcards))

rule align_phenotype:
    input: 
        'process/10X_{cycle}/10X_{cycle}_{{well}}_Tile-{{tile}}.max.tif'.format(cycle=CYCLES[0]),
        'process/10X_{cycle}/10X_{cycle}_{{well}}_Tile-{{tile}}.max.tif'.format(cycle=CYCLE_PHENOTYPE)
    output: 
        'process/10X_{well}_Tile-{tile}.phenotype_aligned.tif'
    run:
        call_firesnake('align_phenotype_2159', output, data_DO=input[0], data_phenotype=input[1])

rule extract_phenotype:
    input:
        'process/10X_{well}_Tile-{tile}.phenotype_aligned.tif',
        'process/10X_{well}_Tile-{tile}.nuclei.tif',
        'process/10X_{well}_Tile-{tile}.cells.tif'
    output:
        'process/10X_{well}_Tile-{tile}.phenotype.csv'
    run:
        call_firesnake('extract_phenotype_FR_HA', output, 
            data_phenotype=input[0], nuclei=input[1], cells=input[2], wildcards=dict(wildcards))
